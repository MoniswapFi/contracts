pragma solidity ^0.8.0;

import "../PriceSource.sol";
import "../../protocol/interfaces/ITradeHelper.sol";
import "@openzeppelin/contracts/token/ERC20/ERC20.sol";

contract MoniswapPriceSource is PriceSource {
    ITradeHelper public immutable tradeHelper;

    constructor(
        ITradeHelper _tradeHelper,
        address _usdt,
        address _usdc,
        address _weth
    ) PriceSource("Moniswap", _usdt, _usdc, _weth) {
        tradeHelper = _tradeHelper;
    }

    function _deriveAmountOut(
        address token0,
        address token1,
        uint256 _amountIn
    ) internal view returns (uint256 amountOut) {
        if (_amountIn == 0) return 0;
        if (token0 == token1) return _amountIn;
        (amountOut, ) = tradeHelper.getAmountOut(_amountIn, token0, token1);
    }

    function _getUnitValueInETH(address token) internal view override returns (uint256 amountOut) {
        uint8 _decimals = ERC20(token).decimals();
        uint256 _amountIn = 1 * 10**_decimals;
        amountOut = _deriveAmountOut(token, weth, _amountIn);
    }

    function _getUnitValueInUSDC(address token) internal view override returns (uint256) {
        uint256 _valueInETH = _getUnitValueInETH(token);
        uint256 _ethUSDCAmountOut = _deriveAmountOut(weth, usdc, _valueInETH);

        if (_ethUSDCAmountOut > 0) {
            return _ethUSDCAmountOut;
        } else {
            uint8 _tokenDecimals = ERC20(token).decimals();
            uint256 _amountIn = 1 * 10**_tokenDecimals;
            uint256 amountOut = _deriveAmountOut(token, usdc, _amountIn);
            return amountOut;
        }
    }

    function _getUnitValueInUSDT(address token) internal view override returns (uint256) {
        uint256 _valueInETH = _getUnitValueInETH(token);
        uint256 _ethUSDTAmountOut = _deriveAmountOut(weth, usdt, _valueInETH);

        if (_ethUSDTAmountOut > 0) {
            return _ethUSDTAmountOut;
        } else {
            uint8 _tokenDecimals = ERC20(token).decimals();
            uint256 _amountIn = 1 * 10**_tokenDecimals;
            uint256 amountOut = _deriveAmountOut(token, usdt, _amountIn);
            return amountOut;
        }
    }
}
